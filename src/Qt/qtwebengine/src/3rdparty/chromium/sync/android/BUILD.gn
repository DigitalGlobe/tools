# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

android_library("sync_java") {
  deps = [
    "//base:base_java",
    "//net/android:net_java",
    "//third_party/android_tools:android_gcm_java",
    "//third_party/cacheinvalidation:cacheinvalidation_javalib",
    "//third_party/cacheinvalidation:cacheinvalidation_proto_java",
    "//third_party/jsr-305:jsr_305_javalib",
    google_play_services_library,
  ]
  srcjar_deps = [ ":java_enums" ]
  java_files = [
    "java/src/org/chromium/sync/AndroidSyncSettings.java",
    "java/src/org/chromium/sync/ModelTypeHelper.java",
    "java/src/org/chromium/sync/PassphraseType.java",
    "java/src/org/chromium/sync/SyncConstants.java",
    "java/src/org/chromium/sync/SyncContentResolverDelegate.java",
    "java/src/org/chromium/sync/SystemSyncContentResolverDelegate.java",
    "java/src/org/chromium/sync/notifier/InvalidationClientNameGenerator.java",
    "java/src/org/chromium/sync/notifier/InvalidationClientNameProvider.java",
    "java/src/org/chromium/sync/notifier/InvalidationIntentProtocol.java",
    "java/src/org/chromium/sync/notifier/InvalidationPreferences.java",
    "java/src/org/chromium/sync/notifier/RandomizedInvalidationClientNameGenerator.java",
    "java/src/org/chromium/sync/signin/AccountManagerDelegate.java",
    "java/src/org/chromium/sync/signin/AccountManagerHelper.java",
    "java/src/org/chromium/sync/signin/AuthException.java",
    "java/src/org/chromium/sync/signin/ChromeSigninController.java",
    "java/src/org/chromium/sync/signin/SystemAccountManagerDelegate.java",
  ]
}

android_library("sync_javatests") {
  testonly = true
  deps = [
    ":sync_java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//sync:sync_java_test_support",
    "//third_party/cacheinvalidation:cacheinvalidation_javalib",
    "//third_party/cacheinvalidation:cacheinvalidation_proto_java",
    "//third_party/jsr-305:jsr_305_javalib",
  ]
  java_files = [
    "javatests/src/org/chromium/sync/AndroidSyncSettingsTest.java",
    "javatests/src/org/chromium/sync/notifier/InvalidationClientNameProviderTest.java",
    "javatests/src/org/chromium/sync/notifier/InvalidationPreferencesTest.java",
    "javatests/src/org/chromium/sync/notifier/signin/AccountManagerHelperTest.java",
  ]
}

# GYP: //sync/sync_android.gypi:model_type_java
java_cpp_enum("java_enums") {
  sources = [
    "//sync/internal_api/public/base/model_type.h",
    "//sync/internal_api/public/base/stop_source.h",
  ]
}

generate_jni("jni_headers") {
  sources = [
    "java/src/org/chromium/sync/ModelTypeHelper.java",
  ]
  jni_package = "syncer"
}
