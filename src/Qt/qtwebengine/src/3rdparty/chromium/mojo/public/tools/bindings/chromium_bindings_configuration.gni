# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

_typemap_imports = [
  "//cc/ipc/typemaps.gni",
  "//device/bluetooth/public/interfaces/typemaps.gni",
  "//components/arc/common/typemaps.gni",
  "//components/typemaps.gni",
  "//gpu/ipc/common/typemaps.gni",
  "//media/mojo/interfaces/typemaps.gni",
  "//mojo/common/typemaps.gni",
  "//mojo/public/cpp/bindings/tests/chromium_typemaps.gni",
  "//skia/public/interfaces/typemaps.gni",
  "//ui/events/devices/mojo/typemaps.gni",
  "//ui/events/mojo/typemaps.gni",
  "//ui/gfx/typemaps.gni",
  "//url/mojo/typemaps.gni",
]
_typemaps = []

foreach(typemap_import, _typemap_imports) {
  _imported = read_file(typemap_import, "scope")
  _typemaps += _imported.typemaps
}

typemaps = []
foreach(typemap, _typemaps) {
  typemaps += [ read_file(typemap, "scope") ]
}
