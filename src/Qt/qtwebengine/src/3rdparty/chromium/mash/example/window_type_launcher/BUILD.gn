# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/mojo_application.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/grit/repack.gni")

executable("window_type_launcher") {
  output_name = "window_type_launcher_exe"
  testonly = true

  sources = [
    "main.cc",
    "window_type_launcher.cc",
    "window_type_launcher.h",
  ]

  deps = [
    ":window_type_launcher_resources",
    "//base",
    "//base:base_static",
    "//base:i18n",
    "//build/config/sanitizers:deps",
    "//build/win:default_exe_manifest",
    "//components/mus/public/cpp",
    "//components/mus/public/interfaces",
    "//mash/public/interfaces",
    "//mash/session/public/interfaces",
    "//mojo/common:common_base",
    "//mojo/edk/system",
    "//mojo/public/cpp/bindings",
    "//services/shell/public/cpp",
    "//services/shell/public/interfaces",
    "//services/shell/runner:init",
    "//services/shell/runner/common",
    "//skia",
    "//ui/aura",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gfx/geometry/mojo",
    "//ui/views",
    "//ui/views/mus",
  ]

  data_deps = [
    ":manifest",
    "//components/mus",
  ]
}

copy("window_type_launcher_resources") {
  sources = [
    "$root_out_dir/views_mus_resources.pak",
  ]
  outputs = [
    "$root_out_dir/Mojo Applications/window_type_launcher_exe/resources/views_mus_resources.pak",
  ]
  deps = [
    "//ui/views/mus:resources",
  ]
}

mojo_application_manifest("manifest") {
  type = "exe"
  application_name = "window_type_launcher_exe"
  source = "manifest.json"
}
