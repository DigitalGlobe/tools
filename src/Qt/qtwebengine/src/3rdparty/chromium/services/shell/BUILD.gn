# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application_manifest.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

group("all") {
  testonly = true
  deps = [
    ":shell",
    "//services/shell/background",
    "//services/shell/runner",
    "//services/shell/standalone",
    "//services/shell/tests",
  ]
}

source_set("shell") {
  output_name = "mojo_shell"
  sources = [
    "connect_params.cc",
    "connect_params.h",
    "connect_util.cc",
    "connect_util.h",
    "native_runner.h",
    "native_runner_delegate.h",
    "shell.cc",
    "shell.h",
    "switches.cc",
    "switches.h",
  ]

  deps = [
    "//base",
    "//base/third_party/dynamic_annotations",
    "//mojo/common",
    "//mojo/public/cpp/bindings",
    "//services/catalog/public/interfaces",
    "//services/shell/public/cpp:sources",
    "//services/shell/public/interfaces",
  ]

  public_deps = [
    # ApplicationManager exposes and uses PackageManager types in its header.
    "//services/catalog:lib",
  ]

  data_deps = [
    ":manifest",
  ]
}

mojo_application_manifest("manifest") {
  application_name = "shell"
  source = "manifest.json"
}
