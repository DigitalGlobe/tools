# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# GYP version: mojo/mojo_base.gyp:mojo_application_base
source_set("cpp") {
  public_deps = [
    ":sources",
  ]
}

# TODO(rockot): Rename this to "cpp".
source_set("sources") {
  sources = [
    "application_runner.h",
    "capabilities.h",
    "connect.h",
    "connection.h",
    "connector.h",
    "identity.h",
    "interface_binder.h",
    "interface_factory.h",
    "interface_factory_impl.h",
    "interface_provider.h",
    "interface_registry.h",
    "lib/application_runner.cc",
    "lib/callback_binder.cc",
    "lib/callback_binder.h",
    "lib/capabilities.cc",
    "lib/connection_impl.cc",
    "lib/connection_impl.h",
    "lib/connector_impl.cc",
    "lib/connector_impl.h",
    "lib/identity.cc",
    "lib/interface_factory_binder.h",
    "lib/interface_provider.cc",
    "lib/interface_registry.cc",
    "lib/names.cc",
    "lib/shell_client.cc",
    "lib/shell_connection.cc",
    "lib/shell_connection_ref.cc",
    "names.h",
    "shell_client.h",
    "shell_connection.h",
    "shell_connection_ref.h",
  ]

  public_deps = [
    "//base",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//services/shell/public/interfaces",
    "//url",
  ]
}

if (!is_ios) {
  source_set("application_support") {
    sources = [
      "lib/init_commandline.cc",
      "lib/initialize_base_and_icu.cc",
    ]

    deps = [
      "//base",
      "//base:i18n",
      "//mojo/public/c/system",
    ]
  }

  source_set("shell_test_support") {
    testonly = true
    sources = [
      "lib/shell_test.cc",
      "shell_test.h",
    ]

    public_deps = [
      ":cpp",
      "//testing/gtest",
    ]

    deps = [
      "//base",
      "//base/test:test_support",
      "//mojo/public/cpp/bindings",
      "//mojo/public/cpp/system",
      "//services/shell/background:lib",
      "//services/shell/public/interfaces:interfaces_cpp_sources",
    ]

    data_deps = []
  }
}
