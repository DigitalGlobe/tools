# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//testing/test.gni")

source_set("gpu") {
  output_name = "mus_gpu"

  sources = [
    "gpu_service_impl.cc",
    "gpu_service_impl.h",
    "gpu_service_mus.cc",
    "gpu_service_mus.h",
    "mus_gpu_memory_buffer_manager.cc",
    "mus_gpu_memory_buffer_manager.h",
  ]

  deps = [
    "//components/mus/common:mus_common",
    "//components/mus/public/interfaces",
    "//gpu/ipc/common",
    "//gpu/ipc/service",
    "//ipc",
    "//media/gpu/ipc/service",
    "//mojo/public/cpp/system",
    "//services/shell/public/cpp",
    "//ui/gfx:memory_buffer",
    "//ui/gl/init",
  ]

  if (use_ozone) {
    deps += [ "//ui/ozone:ozone" ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":mus_gpu_unittests",
  ]
}

test("mus_gpu_unittests") {
  deps = [
    ":gpu",
    "//base",
    "//components/mus/common:run_all_shelltests",
    "//ipc",
    "//testing/gtest",
    "//ui/gfx:memory_buffer",
    "//ui/gfx/geometry",
  ]

  data_deps = [
    ":mus_gpu_unittests_app_manifest",
  ]
}

mojo_application_manifest("mus_gpu_unittests_app_manifest") {
  application_name = "mus_gpu_unittests_app"
  source = "mus_gpu_unittests_app_manifest.json"
}
