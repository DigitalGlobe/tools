# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//testing/test.gni")

if (is_android) {
  # GYP: components.gyp:web_restrictions_java
  android_library("web_restrictions_java") {
    java_files = [
      "browser/java/src/org/chromium/components/webrestrictions/WebRestrictionsContentProvider.java",
      "browser/java/src/org/chromium/components/webrestrictions/WebRestrictionsClient.java",
    ]
    deps = [
      "//base:base_java",
    ]
  }

  # GYP: //components/components_test.gyp:components_junit_tests
  junit_binary("components_web_restrictions_junit_tests") {
    java_files = [
      "browser/junit/src/org/chromium/components/webrestrictions/WebRestrictionsContentProviderTest.java",
      "browser/junit/src/org/chromium/components/webrestrictions/WebRestrictionsClientTest.java",
    ]
    deps = [
      ":web_restrictions_java",
      "//base:base_java",
      "//third_party/junit:hamcrest",
    ]
  }

  generate_jni("web_restrictions_jni_headers") {
    sources = [
      "browser/java/src/org/chromium/components/webrestrictions/WebRestrictionsClient.java",
    ]
    jni_package = "web_restrictions"
  }

  # GYP: components.gyp:web_restrictions_browser
  static_library("browser") {
    sources = [
      "browser/web_restrictions_client.cc",
      "browser/web_restrictions_client.h",
      "browser/web_restrictions_resource_throttle.cc",
      "browser/web_restrictions_resource_throttle.h",
    ]
    deps = [
      ":web_restrictions_jni_headers",
      "//base",
      "//content/public/browser",
      "//net",
    ]
  }

  # GYP: components.gyp:web_restrictions_renderer
  static_library("renderer") {
    sources = [
      "renderer/web_restrictions_gin_wrapper.cc",
      "renderer/web_restrictions_gin_wrapper.h",
    ]
    deps = [
      "//base",
      "//content/public/renderer",
      "//gin",
      "//third_party/WebKit/public:blink_headers",
    ]
  }

  generate_jni("test_support_jni_headers") {
    sources = [
      "browser/javatest/src/org/chromium/components/webrestrictions/MockWebRestrictionsClient.java",
    ]
    jni_package = "web_restrictions"
    deps = [
      ":web_restrictions_java",
    ]
  }

  android_library("test_support_java") {
    java_files = [ "browser/javatest/src/org/chromium/components/webrestrictions/MockWebRestrictionsClient.java" ]
    deps = [
      ":web_restrictions_java",
      "//base:base_java",
    ]
  }

  source_set("test_support") {
    sources = [
      "browser/mock_web_restrictions_client.cc",
      "browser/mock_web_restrictions_client.h",
    ]
    deps = [
      ":test_support_jni_headers",
      "//base",
    ]
  }

  source_set("unit_tests") {
    testonly = true
    sources = [
      "browser/web_restrictions_client_unittest.cc",
      "browser/web_restrictions_resource_throttle_unittest.cc",
    ]
    deps = [
      ":browser",
      ":test_support",
      "//base",
      "//base/test:test_support",
      "//content/test:test_support",
      "//net",
      "//testing/gtest",
      "//url",
    ]
  }
}
