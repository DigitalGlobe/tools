# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/compiler/compiler.gni")
import("//build/config/sysroot.gni")
import("//build/toolchain/gcc_toolchain.gni")
import("//build/toolchain/cros_toolchain.gni")

# TODO(dpranke): Delete this after we get rid of the reference to
# build/toolchain/cros:clang_target in BUILDCONFIG.gn
clang_toolchain("clang_target") {
  toolchain_cpu = target_cpu
  toolchain_os = "linux"
}

template("cros_target_toolchain") {
  if (defined(invoker.use_debug_fission)) {
    use_debug_fission = invoker.use_debug_fission
  }
  if (defined(invoker.use_gold)) {
    use_gold = invoker.use_gold
  }
  if (defined(invoker.use_sysroot)) {
    use_sysroot = invoker.use_sysroot
  }

  gcc_toolchain(target_name) {
    # These are args for the template.
    ar = cros_target_ar
    cc = cros_target_cc
    cxx = cros_target_cxx
    ld = cxx
    if (cros_target_ld != "") {
      ld = cros_target_ld
    }
    if (cros_target_nm != "") {
      nm = cros_target_nm
    }
    if (cros_target_readelf != "") {
      readelf = cros_target_readelf
    }
    extra_cflags = cros_target_extra_cflags
    extra_cppflags = cros_target_extra_cppflags
    extra_cxxflags = cros_target_extra_cxxflags
    extra_ldflags = cros_target_extra_ldflags

    # These are passed through as toolchain_args.
    cc_wrapper = ""
    is_clang = is_clang
    toolchain_cpu = target_cpu
    toolchain_os = "chromeos"

    use_debug_fission = use_debug_fission
    use_gold = use_gold
    use_sysroot = use_sysroot
  }
}

# This is the normal toolchain for most targets.
cros_target_toolchain("target") {
}

# This is a special toolchain needed just for the nacl_bootstrap target in
# //native_client/src/trusted/service_runtime/linux. It is identical
# to ":target" except that it forces use_debug_fission, use_gold, and
# use_sysroot off.
cros_target_toolchain("nacl_bootstrap") {
  use_debug_fission = false
  use_gold = false
  use_sysroot = false
}

gcc_toolchain("host") {
  # These are args for the template.
  ar = cros_host_ar
  cc = cros_host_cc
  cxx = cros_host_cxx
  ld = cxx
  if (cros_host_ld != "") {
    ld = cros_host_ld
  }
  if (cros_host_nm != "") {
    nm = cros_host_nm
  }
  if (cros_host_readelf != "") {
    readelf = cros_host_readelf
  }
  extra_cflags = cros_host_extra_cflags
  extra_cppflags = cros_host_extra_cppflags
  extra_cxxflags = cros_host_extra_cxxflags
  extra_ldflags = cros_host_extra_ldflags

  # These are passed through as toolchain_args.
  cc_wrapper = ""
  is_clang = cros_host_is_clang
  toolchain_cpu = host_cpu
  toolchain_os = "linux"
  use_sysroot = false
}

gcc_toolchain("v8_snapshot") {
  # These are args for the template.
  ar = cros_v8_snapshot_ar
  cc = cros_v8_snapshot_cc
  cxx = cros_v8_snapshot_cxx
  ld = cxx
  if (cros_v8_snapshot_ld != "") {
    ld = cros_v8_snapshot_ld
  }
  if (cros_v8_snapshot_nm != "") {
    nm = cros_v8_snapshot_nm
  }
  if (cros_v8_snapshot_readelf != "") {
    readelf = cros_v8_snapshot_readelf
  }
  extra_cflags = cros_v8_snapshot_extra_cflags
  extra_cppflags = cros_v8_snapshot_extra_cppflags
  extra_cxxflags = cros_v8_snapshot_extra_cxxflags
  extra_ldflags = cros_v8_snapshot_extra_ldflags

  # These are passed through as toolchain_args.
  cc_wrapper = ""
  is_clang = cros_v8_snapshot_is_clang
  if (target_cpu == "x86" || target_cpu == "arm" || target_cpu == "mipsel") {
    toolchain_cpu = "x86"
  } else {
    toolchain_cpu = "x64"
  }
  toolchain_os = "linux"
  use_sysroot = false
}
